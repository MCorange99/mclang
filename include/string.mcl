macro sizeof(Str) 16 end
macro Str.count   0 + end
macro Str.data    8 + end
macro @Str.count  Str.count @64 end
macro @Str.data   Str.data  @64 cast(ptr) end
macro !Str.count  Str.count !64 end
macro !Str.data   Str.data  !64 end

macro @Str
  dup @Str.count
  swap @Str.data
end

macro !Str // count data dst -
  dup rot swap
  !Str.data
  !Str.count
end

macro String.chop_one_left
   dup Str.count dec64
       Str.data inc64
end

macro String.trim_left // input --
  while 
    if dup @Str.count 0 > do
       dup @Str.data @8 ' ' =
    else false end
  do
    dup String.chop_one_left
  end
  drop
end

macro String.chop_line // line input --
  2dup @Str.data swap !Str.data
  over 0 swap !Str.count
  while
    if dup @Str.count 0 > do
       dup @Str.data @8 '\n' !=
    else false end
  do
    dup String.chop_one_left
    swap dup Str.count inc64 swap
  end
  if dup @Str.count 0 > do
    dup String.chop_one_left
  end
  2drop
end

// TODO: merge str-chop-word and str-chop-line into a single macro
macro String.chop_word // line input --
  2dup @Str.data swap !Str.data
  over 0 swap !Str.count
  while
    if dup @Str.count 0 > do
       dup @Str.data @8 ' ' !=
    else false end
  do
    dup String.chop_one_left
    swap dup Str.count inc64 swap
  end
  if dup @Str.count 0 > do
    dup String.chop_one_left
  end
  2drop
end

macro cstrlen
  dup
  while dup @8 0 != do 1 + end
  swap -
end

macro cstreq
  while
    if over @8 0 != over @8 0 != and do
       over @8 over @8 =
    else
       false
    end
  do
    1 + swap 1 +
  end
  @8 0 =
  swap @8 0 =
  and
end

macro cstr-to-str
  dup cstrlen swap
end

memory streq_a sizeof(Str) end
memory streq_b sizeof(Str) end
macro streq // n1 s1 n2 s2
  streq_a !Str
  streq_b !Str
  if streq_a @Str.count streq_b @Str.count = do
    0 while
      if dup streq_a @Str.count < do
        dup streq_a @Str.data + @8
        over streq_b @Str.data + @8
        =
      else false end
    do 1 + end
    streq_a @Str.count >=
  else false end
end

macro isdigit
  dup  '0' >=
  swap '9' <=
  and
end